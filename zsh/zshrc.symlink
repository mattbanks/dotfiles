#!/bin/zsh
# shortcut to this dotfiles path is $DOTFILES
export DOTFILES="$HOME/.dotfiles"

# your project folder that we can `c [tab]` to
export PROJECTS="$HOME/Developer"

# your default editor
export EDITOR='vim'
export VEDITOR='code'

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# disable brew analytics
export HOMEBREW_NO_ANALYTICS=1

# disable git maintenance for znap
zstyle ':znap:*:*' git-maintenance off

# load znap plugins
source $DOTFILES/znap/znap.sh

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

autoload -Uz compinit
for dump in ~/.zcompdump(N.mh+24); do
  compinit
done
compinit -C


# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

# uv completions
eval "$(uv generate-shell-completion zsh)"

unset config_files updated_at

# pull in dotfiles-private zsh aliases and data
# shellcheck disable=SC1090
[ -f ~/.dotfiles-private/zsh/private.zsh ] && . ~/.dotfiles-private/zsh/private.zsh

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
# shellcheck disable=SC1090
[ -f ~/.localrc ] && . ~/.localrc
